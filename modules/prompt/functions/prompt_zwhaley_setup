function prompt_zwhaley_precmd {
  vcs_info
}

function prompt_zwhaley_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_zwhaley_precmd

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable git hg p4
  zstyle ':vcs_info:p4*' formats '%F{blue}%s'
  zstyle ':vcs_info:(git|hg)*' formats '%F{blue}%s%f:%F{green}%b'

  SUCCESS_PROMPT=%F{blue}❯%F{cyan}❯%F{green}❯%f
  FAILURE_PROMPT=%F{blue}❯%F{magenta}❯%F{red}❯%f

  # Define prompts.
  PROMPT='%B%F{magenta}[%b%F{cyan}%~%B%F{magenta}]%b
%(?.${SUCCESS_PROMPT}.${FAILURE_PROMPT}) '
  RPROMPT='${vcs_info_msg_0_}'

}

prompt_zwhaley_setup "$@"
